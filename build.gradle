plugins {
    id "java"
    id "edu.wpi.first.GradleRIO" version "2020.3.2"
    id "com.github.johnrengelman.shadow" version "5.2.0"
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

def ROBOT_MAIN_CLASS = "frc.robot.Main"

// VMX Pi Deployment Configuration
deploy {
    targets {
        target("vmxpi", jaci.gradle.deploy.target.RemoteTarget) {
            directory = '/home/pi/deploy'
            maxChannels = 1
            locations {
                ssh(jaci.gradle.deploy.target.location.SshDeployLocation) {
                    address = "172.22.11.2"
                    user = 'pi'
                    password = ''
                    ipv6 = false
                }
            }
        }
    }
    artifacts {
        frcJavaArtifact('frcJava') {
            targets << "vmxpi"
        }
        fileTreeArtifact('frcStaticFileDeploy') {
            targets << "vmxpi"
            files = fileTree(dir: 'src/main/deploy')
            directory = '/home/pi/deploy'
        }
    }
}

// Defining my dependencies
dependencies {
    implementation wpi.deps.wpilib()
    nativeZip wpi.deps.wpilibJni(wpi.platforms.roborio)
    nativeDesktopZip wpi.deps.wpilibJni(wpi.platforms.desktop)

    implementation wpi.deps.vendor.java()
    nativeZip wpi.deps.vendor.jni(wpi.platforms.roborio)
    nativeDesktopZip wpi.deps.vendor.jni(wpi.platforms.desktop)

    testImplementation 'junit:junit:4.12'

    simulation wpi.deps.sim.gui(wpi.platforms.desktop, false)
}

// Setting up my Jar File
jar {
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest edu.wpi.first.gradlerio.GradleRIOPlugin.javaManifest(ROBOT_MAIN_CLASS)
}
